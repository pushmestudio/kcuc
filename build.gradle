apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'war'
apply plugin: 'maven'

// version = '1.0' // versionをつけると、生成されるwarの末尾にversionが付く
def defaultEncoding = 'UTF-8'
def deployTargetDir = '/opt/tomcat/webapps'
tasks.withType(AbstractCompile).each {
  it.options.encoding = defaultEncoding
}
tasks.withType(GroovyCompile).each {
  it.groovyOptions.encoding = defaultEncoding
}
[compileJava, compileTestJava].each {
  it.options.compilerArgs += ['-source', '1.8', '-target', '1.8']
}

repositories {
  mavenCentral()
  flatDir dirs: "${webAppDir}/WEB-INF/lib"
}

dependencies {
  // GroupId:ArtifactId:Versionの順で書く

  // ローカルファイル
  compile ':org.JSON-JSON-java:'

  // HttpServletなどを使用する場合には必要
  // providedCompile 'javax.servlet:javax.servlet-api:3.1'

  compile 'org.glassfish.jersey.core:jersey-server:2.23'
  compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.23'

  // swagger
  compile 'io.swagger:swagger-jersey2-jaxrs:1.5.10'

  // cloudant
  compile 'com.cloudant:cloudant-client:2.7.0'

  // glassfish containerを使う場合はjersey-container-servletを消して下記を有効化する
  // compile 'org.glassfish.jersey.containers:jersey-container-jdk-http:2.23'

  testCompile 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-jdk-http:2.23'
  testCompile 'junit:junit:4.12'
}

// enclosed testが2回実行されることを防ぐ
test {
  exclude '**/*$*'
}
war {
  archiveName 'kcuc.war'
}

// pom.xml書き出し、ここできちんと書き出させるためにはdependenciesをまともに書く必要あり
task writePom {
  doLast {
    pom().writeTo('pom.xml')
  }
}

// 生成されたwarを配置する
task deploy(type: Exec) {
  workingDir "${projectDir}/build/libs/"
  commandLine 'cp', '-v', 'kcuc.war', "${deployTargetDir}/ROOT.war"
}
